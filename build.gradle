buildscript {
	ext.kotlin_version = '1.0.5-3'

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'jacoco'

group 'easymaps-engine'
version '1.0.0'
mainClassName = 'uk.co.markormesher.easymaps.engine.EntryPointKt'

/* testing  */

test {
	testLogging {
		events "skipped", "failed", "passed"
		exceptionFormat "full"
	}

	jacoco {
		append = false
		destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
		classDumpFile = file("$buildDir/jacoco/classpathdumps")
	}
}

jacocoTestReport {
	reports {
		xml.enabled false
		csv.enabled false
		html.destination "${buildDir}/reports/jacoco"
	}
}

task testEnd << {
	println()
	println("Test results in file://${buildDir}/reports/tests/test/index.html")
	println("Coverage results in file://${buildDir}/reports/jacoco/index.html")
}

test.finalizedBy(jacocoTestReport)
test.finalizedBy(testEnd)

/* compiling */

task fatJar(type: Jar) {
	manifest {
		attributes 'Implementation-Title': 'EasyMaps Engine',
				'Implementation-Version': version,
				'Main-Class': mainClassName
	}
	baseName = project.name
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}

/* dependencies */

repositories {
	mavenCentral()
}

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	compile 'com.google.guava:guava:20.0'

	testCompile 'junit:junit:4.11'
	testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}
